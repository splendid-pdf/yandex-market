import com.google.cloud.tools.jib.api.buildplan.ImageFormat

plugins {
    id 'com.marketplace.common'
    id 'com.marketplace.common.spring'
    alias libs.plugins.jib
    alias libs.plugins.spring.boot
    alias libs.plugins.spring.dependency.management
}

dependencies {
    implementation project(':commons')

    implementation libs.spring.web
    implementation libs.spring.redis
    implementation libs.spring.data.jpa
    implementation libs.spring.security
    implementation libs.spring.oauth2.resource.server

    implementation libs.spring.cloud.config.client

    implementation libs.flyway
    runtimeOnly libs.postgresql

    developmentOnly libs.spring.devtools

    testImplementation libs.h2.database
    testImplementation libs.spring.test
    testImplementation libs.mockito.inline
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$libs.versions.spring.cloud"
    }
}

jib {
    from {
        image = 'gcr.io/distroless/java17-debian11'
    }
    to {
        image = "${dockerHubUsername}/${project.name}:${project.version}"
        tags = [version, project.version] as List<String>
        auth {
            username = dockerHubUsername
            password = dockerHubPassword
        }
    }
    container {
        format = ImageFormat.OCI
    }
}