import com.google.cloud.tools.jib.api.buildplan.ImageFormat

plugins {
    id 'com.marketplace.common'
    alias libs.plugins.jib
    alias libs.plugins.flyway
    alias libs.plugins.spring.boot
    alias libs.plugins.spring.dependency.management
}

ext {
    set('flywayCoreVersion', '9.14.1')
    set("springCloudConfigVersion", "2022.0.1")
}

dependencies {
    implementation project(':commons')

    // spring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //spring cloud
    implementation 'org.springframework.cloud:spring-cloud-config-client'

    // pdf
    implementation 'com.itextpdf:itextpdf:5.5.13.3'

    // open api
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    // db
    runtimeOnly 'org.postgresql:postgresql'
    implementation "org.flywaydb:flyway-core:${flywayCoreVersion}"

    // monitoring
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus:1.10.5'

    // annotation processor
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.6'
    testImplementation 'org.testcontainers:postgresql:1.17.6'
}


dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudConfigVersion}"
    }
}

jib {
    from {
        image = 'gcr.io/distroless/java17-debian11'
    }
    to {
        image = "${dockerHubUsername}/${project.name}:${project.version}"
        tags = [version, project.getVersion()] as List<String>
        auth {
            username = dockerHubUsername
            password = dockerHubPassword
        }
    }
    container {
        format = ImageFormat.OCI
    }
}