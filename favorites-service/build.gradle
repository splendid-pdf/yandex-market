plugins {
    id 'pmd'
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
}

ext {
    set('mapstruct', '1.5.3.Final')
    set('openapi', '2.0.2')
    set('commonslang3', '3.12.0')
    set('projectlombok', '1.18.24')
    set('testcontainersjunit', '1.17.6')
    set('mockitoInlineVersion', '4.10.0')
    set('testcontainerspostgresql', '1.17.6')
    set('H2', '2.1.214')
    set('assertJVersion', '3.24.2')
    set("springCloudConfigVersion", "2022.0.1")
}

dependencies {
    implementation project(':commons')

    // spring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    //spring cloud
    implementation 'org.springframework.cloud:spring-cloud-config-client'

    // mapper
    implementation "org.mapstruct:mapstruct:${mapstruct}"

    // open api
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${openapi}"

    // utils
    implementation "org.apache.commons:commons-lang3:${commonslang3}"

    // lombok
    compileOnly 'org.projectlombok:lombok'
    testCompileOnly "org.projectlombok:lombok:${projectlombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${projectlombok}"

    // db
    runtimeOnly 'org.postgresql:postgresql'

    // monitoring
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // annotation processor
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct}"

    // tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testImplementation "org.assertj:assertj-core:${assertJVersion}"
    testImplementation "org.mockito:mockito-inline:${mockitoInlineVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersjunit}"
    testImplementation "org.testcontainers:postgresql:${testcontainerspostgresql}"
    testImplementation "com.h2database:h2:${H2}"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudConfigVersion}"
    }
}

bootBuildImage {
    def account = rootProject.property('dockerHubUsername')
    imageName = "${account}/${project.name}:${project.version}"
    publish = true
    environment = [
            "BP_JVM_VERSION" : "${compileJava.sourceCompatibility}"
    ]
    docker {
        publishRegistry {
            username = account
            password = rootProject.property('dockerHubPassword')
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

pmd {
    toolVersion = '6.53.0'
    consoleOutput = true
    ignoreFailures = true
    ruleSetFiles = files("${rootDir}/ruleset.xml")
    ruleSets = []
}