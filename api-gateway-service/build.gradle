import com.google.cloud.tools.jib.api.buildplan.ImageFormat

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'com.google.cloud.tools.jib' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

ext {
    set('springCloudVersion', "2022.0.1")
}

dependencies {

    // spring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // spring cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'

    // utils
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    // tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

jib {
    var tag = project.version
    from {
        image = "gcr.io/distroless/java17-debian11"
    }
    to {
        image = "splendidpdf/${project.name}:$tag"
    }
    container {
        creationTime = Instant.now().toString()
        format = ImageFormat.OCI
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
